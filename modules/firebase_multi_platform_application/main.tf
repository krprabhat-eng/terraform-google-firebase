/**
 * Copyright 2026 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

resource "google_project_service" "firebase_apis" {
  for_each = toset([
    "firebase.googleapis.com",
    "serviceusage.googleapis.com",
    "cloudresourcemanager.googleapis.com",
    "identitytoolkit.googleapis.com"
  ])
  project            = var.project_id
  service            = each.key
  disable_on_destroy = false
}

resource "google_firebase_project" "default" {
  provider = google-beta
  project  = var.project_id

  depends_on = [
    google_project_service.firebase_apis
  ]
}

resource "google_firebase_web_app" "default" {
  count        = var.web_app != null ? 1 : 0
  provider     = google-beta
  project      = google_firebase_project.default.project
  display_name = var.display_name
  api_key_id   = var.web_app.api_key_id
}

resource "google_firebase_android_app" "default" {
  count         = var.android_app != null ? 1 : 0
  provider      = google-beta
  project       = google_firebase_project.default.project
  display_name  = var.display_name
  package_name  = var.android_app.package_name
  sha256_hashes = var.android_app.sha256_hashes
}

resource "google_firebase_apple_app" "default" {
  count        = var.apple_app != null ? 1 : 0
  provider     = google-beta
  project      = google_firebase_project.default.project
  display_name = var.display_name
  bundle_id    = var.apple_app.bundle_id
  team_id      = var.apple_app.team_id
}

data "google_firebase_web_app_config" "default" {
  count      = var.web_app != null ? 1 : 0
  provider   = google-beta
  project    = google_firebase_project.default.project
  web_app_id = google_firebase_web_app.default[0].app_id
}

data "google_firebase_android_app_config" "default" {
  count    = var.android_app != null ? 1 : 0
  provider = google-beta
  project  = google_firebase_project.default.project
  app_id   = google_firebase_android_app.default[0].app_id
}

data "google_firebase_apple_app_config" "default" {
  count    = var.apple_app != null ? 1 : 0
  provider = google-beta
  project  = google_firebase_project.default.project
  app_id   = google_firebase_apple_app.default[0].app_id
}
